buildscript {
    ext.kotlin_version = "1.9.24"
    ext.jacocoVersion = '0.8.7'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        if (System.getenv("SHOULD_PUBLISH") == "true") {
            classpath("io.github.gradle-nexus:publish-plugin:1.1.0")
        }
        classpath 'com.android.tools.build:gradle:8.7.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jacoco:org.jacoco.core:$jacocoVersion"
    }
}

plugins {
    id "org.sonarqube" version "3.5.0.2730"
}

sonarqube {
    properties {
        property "sonar.projectKey", "OutSystems_OSBarcodeLib-Android"
        property "sonar.organization", "outsystemsrd"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

if (System.getenv("SHOULD_PUBLISH") == "true") {
    apply plugin: "io.github.gradle-nexus.publish-plugin"
    apply from: file("./scripts/publish-root.gradle")
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "jacoco"

android {

    namespace "com.outsystems.plugins.barcode"
    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

        reports {
            xml.getRequired().set(true)
            html.getRequired().set(true)
        }

        def fileFilter = ['**/BuildConfig.*', '**/Manifest*.*']
        def debugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
        def mainSrc = "${project.projectDir}/src/main/kotlin"

        sourceDirectories.setFrom(files([mainSrc]))
        classDirectories.setFrom(files([debugTree]))
        executionData.setFrom(fileTree(dir: "$buildDir", includes: [
                "jacoco/testDebugUnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ]))
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.14'
    }

    packaging {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    publishing {
        singleVariant("release")
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation "androidx.compose.ui:ui:1.7.5"
    implementation "androidx.compose.material:material:1.7.5"
    implementation "androidx.compose.ui:ui-tooling-preview:1.7.5"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.8.7'
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation 'androidx.compose.material3:material3:1.3.1'
    implementation 'androidx.compose.material3:material3-window-size-class:1.3.1'
    implementation 'androidx.window:window:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.0.5"
    debugImplementation "androidx.compose.ui:ui-tooling:1.0.5"

    implementation "androidx.camera:camera-camera2:1.4.0"
    implementation 'androidx.camera:camera-lifecycle:1.4.0'
    implementation 'androidx.camera:camera-view:1.4.0'
    implementation 'androidx.camera:camera-core:1.4.0'
    implementation 'com.google.zxing:core:3.4.1'
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation 'com.google.code.gson:gson:2.10.1'

    testImplementation "org.mockito:mockito-core:4.3.0"
    testImplementation 'org.mockito:mockito-inline:4.3.0'
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.2"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2"
}

if (System.getenv("SHOULD_PUBLISH") == "true") {
    apply from: file("./scripts/publish-module.gradle")
}